fs     = require 'fs'
{exec} = require 'child_process'

appFiles  = [
	# omit src/ and .coffee to make the below lines a little shorter
	'facebook-lib'
	'app'
	'ResizeCommand'
	'rotate'
	'rotate-slider'
	'collage'
	'photo'
	'toolbar'
	'main'
]

task 'build', 'Build single application file from source files', ->
	appContents = new Array remaining = appFiles.length
	for file, index in appFiles then do (file, index) ->
		fs.readFile "src/#{file}.coffee", 'utf8', (err, fileContents) ->
			throw err if err
			appContents[index] = fileContents
			process() if --remaining is 0
	process = ->
		fs.writeFile 'client.coffee', appContents.join('\n\n'), 'utf8', (err) ->
			throw err if err
			exec 'coffee -o public/javascripts -c client.coffee', (err, stdout, stderr) ->
				throw err if err
				console.log stdout + stderr
				fs.unlink 'client.coffee', (err) ->
					throw err if err
					console.log 'Done.'

task 'watch', 'Watch prod source files and build changes', ->
	for file in appFiles then do (file) ->
			fs.watchFile "src/#{file}.coffee", (curr, prev) ->
					if +curr.mtime isnt +prev.mtime
						console.log("compiling #{file}.coffee")
						invoke 'build'
